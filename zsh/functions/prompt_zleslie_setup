
function prompt_zleslie_precmd {
  vcs_info 'prompt'
}

function prompt_zleslie_setup {
  setopt LOCAL_OPTIONS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_zleslie_precmd

  # 001 # red
  # 012 # cyan
  # 068 # blueish
  # 074 # blueish/green
  # 234 # darker grey
  # 237 # lighter grey
  # 244 # base grey

  # Define the color variables
  local accent_color='%F{012}' #cyan
  local muted_color='%F{244}' #grey
  local info_color='%F{074}'
  local info_accent_color='%K{270}%F{068}' #blueish
  local attention_color='%K{234}%F{165}'
  local error_color='%F{001}'
  local reset_color='%f%k'

  local fmt_branch="${info_color} %b${info_accent_color} %7.7i ${attention_color}%c%u${reset_color}"
  local fmt_action="(${accent_color}%a%f%)"       # e.g. (rebase-i)
  local fmt_path="${muted_color}%R${accent_color}/%S${reset_color}"  # e.g. ~/repo/subdir
  local fmt_nvcspath="${muted_color}%~${reset_color}"

  # set formats
  # %b - branchname
  # %u - unstagedstr (see below)
  # %c - stagedstr (see below)
  # %a - action (e.g. rebase-i)
  # %R - repository path
  # %r - repository shortname
  # %S - path in the repository

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:prompt:*' get-revision true
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr '⚏'  # display ¹ if there are unstaged changes
  zstyle ':vcs_info:*:prompt:*' stagedstr '⚌'    # display ² if there are staged changes
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch}${fmt_action}" "${fmt_path}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"              "${fmt_path}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   " "                            "${fmt_nvcspath}"

  if [ -n $SSH_TTY ]; then
    local at="${attention_color}@${reset_color}"
  else
    local at="${muted_color}@${reset_color}"
  fi

  local user_host="${muted_color}%n${at}${muted_color}%m${accent_color}:%f"
  local max_path_chars=30
  local user_char='❯'
  local root_char='❯❯❯'

  PROMPT="${SSH_TTY:+${user_host}}"
  PROMPT+='${vcs_info_msg_1_}'
  PROMPT+=" ${error_color}%(?..exit %?)${reset_color}"
  PROMPT+=$'\n'
  PROMPT+="${accent_color}%(!.${root_char}.${user_char})%f "

  RPROMPT='${vcs_info_msg_0_}'
}

prompt_zleslie_setup "$@"
